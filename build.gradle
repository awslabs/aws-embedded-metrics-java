/*
* Copyright 2020 Amazon.com, Inc. or its affiliates.
* Licensed under the Apache License, Version 2.0 (the
* "License"); you may not use this file except in compliance
* with the License.  You may obtain a copy of the License at
*     http://www.apache.org/licenses/LICENSE-2.0
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*
*/


plugins {
	id 'java-library'
	id 'com.diffplug.spotless' version '5.8.2'
	id 'maven-publish'
	id 'signing'
}

group "software.amazon.cloudwatchlogs"

allprojects {
	compileJava {
		sourceCompatibility = '1.8'
		targetCompatibility = '1.8'
	}

	version = '3.0.0-beta-1'
}

java {
	withJavadocJar()
	withSourcesJar()
}

repositories {
	jcenter()
	mavenCentral()
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integration-test/java')
		}
		resources.srcDir file('src/integration-test/resources')
	}
}

configurations {
	integrationTestImplementation.extendsFrom testImplementation
}

dependencies {
	annotationProcessor 'org.projectlombok:lombok:1.18.12'

	compileOnly 'org.projectlombok:lombok:1.18.12'
	implementation 'com.fasterxml.jackson.core:jackson-core:2.11.1'
	implementation 'com.fasterxml.jackson.core:jackson-annotations:2.11.1'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.11.1'
	implementation 'org.slf4j:slf4j-api:1.7.30'
	implementation 'org.javatuples:javatuples:1.2'

	// Use JUnit test framework
	testImplementation 'software.amazon.awssdk:cloudwatch:2.13.54'
	testImplementation 'junit:junit:4.13'
	testImplementation 'org.apache.commons:commons-lang3:3.10'
	testImplementation "org.mockito:mockito-core:2.+"
	testImplementation "org.powermock:powermock-module-junit4:2.0.2"
	testImplementation "org.powermock:powermock-api-mockito2:2.0.2"
	testImplementation "com.github.javafaker:javafaker:1.0.2"
	testImplementation "com.github.tomakehurst:wiremock-jre8:2.27.0"
	testImplementation 'software.amazon.awssdk:cloudwatch:2.13.54'
	testCompileOnly 'org.projectlombok:lombok:1.18.12'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
}

spotless {
	format 'misc', {
		target '*.gradle', '*.md', '.gitignore'

		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}

	java {
		importOrder()
		googleJavaFormat('1.7').aosp()
		removeUnusedImports()

	}
}

test {
	outputs.upToDateWhen {false}
}

jar {
	manifest {
		attributes 'Implementation-Version': archiveVersion.get()
	}
}


task integrationTest(type: Test) {
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	testLogging.showStandardStreams = true
}

task integ(type: Exec) {
	commandLine './bin/run-integ-tests.sh'
}


tasks.withType(JavaCompile) {
	options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

tasks.named('wrapper') {
	gradleVersion = '6.5.1'
	distributionType = Wrapper.DistributionType.ALL
}

def repoUrl = "https://aws.oss.sonatype.org/service/local/staging/deploy/maven2/"
def repoPassword = System.getenv('mavenPassword')
def repoUserName = System.getenv('mavenUserName')
def buildNumber = System.getenv('CODEBUILD_BUILD_NUMBER')


publishing {
	publications {
		mavenJava(MavenPublication) {
			ext.get_version = {
				if (buildNumber == null) {
					return version + ".dev"
				} else {
					return version + "." + buildNumber
				}
			}
			//TODO: add the build version properly
			//version = get_version()
			from components.java
			versionMapping {
				usage('java-api') {
					fromResolutionOf('runtimeClasspath')
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}
			pom {
				name = 'aws-embedded-metrics'
				description = 'aws-embedded-metrics for java'
				url = 'https://github.com/awslabs/aws-embedded-metrics-java'
				scm {
					url = 'https://github.com/awslabs/aws-embedded-metrics-java'
					connection = 'https://github.com/awslabs/aws-embedded-metrics-java'
					developerConnection = 'https://github.com/awslabs/aws-embedded-metrics-java'
				}
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'https://github.com/awslabs/aws-embedded-metrics-java/blob/master/LICENSE'
					}
				}
				developers {
					developer {
						id = 'aws_emf'
						name = 'AWS CloudWatch'
						email = 'logs-emf@amazon.com'
					}
				}
			}
		}
	}
	repositories {
		maven {
			name = "mavenRepo"
			url = repoUrl
			credentials {
				username = repoUserName
				password = repoPassword
			}
		}
	}
}

signing {
	def signingKey = findProperty("signingKey")
	def signingPassword = findProperty("signingPassword")
	useInMemoryPgpKeys(signingKey, signingPassword)
	sign publishing.publications.mavenJava
}
