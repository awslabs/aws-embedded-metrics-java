/*
* Copyright 2020 Amazon.com, Inc. or its affiliates.
* Licensed under the Apache License, Version 2.0 (the
* "License"); you may not use this file except in compliance
* with the License.  You may obtain a copy of the License at
*     http://www.apache.org/licenses/LICENSE-2.0
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*
*/


plugins {
	// Apply the java-library plugin to add support for Java Library
	id 'java-library'
	id "com.diffplug.spotless" version "5.1.0"
}


allprojects {
	compileJava {
		sourceCompatibility = '1.8'
		targetCompatibility = '1.8'
	}

	version = '0.1.0-beta'
}

repositories {
	// Use jcenter for resolving dependencies.
	// You can declare any Maven/Ivy/file repository here.
	jcenter()
	mavenCentral()
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integration-test/java')
		}
		resources.srcDir file('src/integration-test/resources')
	}
}

configurations {
	integrationTestImplementation.extendsFrom testImplementation
}

dependencies {
	annotationProcessor 'org.projectlombok:lombok:1.18.12'

	implementation 'org.projectlombok:lombok:1.18.12'
	implementation 'com.fasterxml.jackson.core:jackson-core:2.11.1'
	implementation 'com.fasterxml.jackson.core:jackson-annotations:2.11.1'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.11.1'
	implementation 'org.slf4j:slf4j-api:1.7.30'

	// Use JUnit test framework
	testImplementation 'junit:junit:4.13'
	testImplementation 'org.apache.commons:commons-lang3:3.10'
	testImplementation "org.mockito:mockito-core:2.+"
	testImplementation "org.powermock:powermock-module-junit4:2.0.2"
	testImplementation "org.powermock:powermock-api-mockito2:2.0.2"
	testImplementation "com.github.javafaker:javafaker:1.0.2"
	testImplementation "com.github.tomakehurst:wiremock-jre8:2.27.0"
	testImplementation 'software.amazon.awssdk:cloudwatch:2.13.54'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
}

spotless {
	format 'misc', {
		// define the files to apply `misc` to
		target '*.gradle', '*.md', '.gitignore'

		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}

	java {
		importOrder()
		// apply a specific flavor of google-java-format
		googleJavaFormat('1.7').aosp()
		removeUnusedImports()

	}
}

test {
	outputs.upToDateWhen {false}
}

jar {
	manifest {
		attributes 'Implementation-Version': archiveVersion.get()
	}
}


task integrationTest(type: Test) {
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	testLogging.showStandardStreams = true
}

task integ(type: Exec) {
	commandLine './bin/run-integ-tests.sh'
}


tasks.withType(JavaCompile) {
	options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

tasks.named('wrapper') {
	gradleVersion = '6.5.1'
	distributionType = Wrapper.DistributionType.ALL
}
